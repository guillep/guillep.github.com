---
layout: post
title: Customizing ZeroConf scripts
categories:
- Pharo
- Software Engineering
tags:
- bash
- bash scripts
- Pharo
- Smalltalk
- zeroconf
status: publish
type: post
published: true
summary: ZeroConf scripts are bash scripts that ease the installation of a Pharo environment. Now, as I am working on my Phd on my own environment, I'd like to show you how I customized them to manage my own software with them.
meta:
  geo_public: '0'
  _publicize_pending: '1'
  _publicize_done_external: a:1:{s:8:"facebook";a:1:{i:1479284455;b:1;}}
  _wpas_done_3194664: '1'
  _wpas_mess: Customizing ZeroConf scripts http://wp.me/p2tOYl-4v
  publicize_twitter_user: GuillePolito
  _wpas_done_1260342: '1'
  _wpas_done_1260340: '1'
  _elasticsearch_indexed_on: '2013-07-30 10:36:55'
---
As you may already know, ZeroConf scripts are bash scripts that <a>ease the installation of a Pharo environment</a>. A funny thing about these ZeroConf scripts is that they are seen as bash scripts by a bash terminal, and as simple and minimal html pages by a web browser. These scripts are extensively used to simplify the configuration of Pharo CI jobs. They allow you to easily download many versions of the Pharo image and VM.

As I'm working for my Phd, and have a custom version of my virtual machine and image, and also want to make use of the advantages our CI server provides, I wanted to build my own ZeroConf scripts specialized for my needs. I also heard recently on the pharo mailing list that there was some work on customizing ZeroConf scripts[1] for Moose[2]. So I wanted to do it as well for my project :).

[1] <a href="http://get.moosetechnology.org/">http://get.moosetechnology.org/</a>
[2] <a href="http://www.moosetechnology.org/">http://www.moosetechnology.org/</a>
<h2>Downloading the ZeroConf package</h2>
The ZeroConf scripts are generated automatically by the ZeroConf Pharo package. You can find this package in [2]. To download the current version, you just have to execute the following piece of code in a workspace:

[code language="ruby"]
Gofer it
	smalltalkhubUser: 'Pharo' project: 'ZeroConf';
	package: 'ZeroConf';
	loadVersion: '1.0'
[/code]

This code snippet will install in your image the ZeroConf package for the 1.0 version, containing the script generator, and some tests that are not currently working :).
[2] <a href="http://www.smalltalkhub.com/#!/~Pharo/ZeroConf">http://www.smalltalkhub.com/#!/~Pharo/ZeroConf</a>
<h2>Getting what's inside ZeroConf</h2>
The ZeroConf package is pretty small and simple. There is an abstract class AbstractZeroConfBashScript implementing most of the script generation and bash writing utils. Its subclasses will implement the concrete script generation. Current implementation includes three main classes below the AbstractZeroConfBashScript hierarchy, implementing a composite pattern:
<ul>
	<li><strong>ZeroConfImageScript</strong>: Generates scripts in charge of downloading image files.</li>
	<li><strong>ZeroConfVMScript</strong>: Generates scripts in charge of downloading Virtual Machine files (and source files).</li>
	<li><strong>ZeroConfCombinedScript</strong>: Generates scripts that combine several scripts. It will point to its combined scripts and downloading it means to download them all.</li>
</ul>
<a href="http://playingwithobjects.files.wordpress.com/2013/07/zeroconf.png"><img class="alignright size-large wp-image-281" alt="zeroconf" src="http://playingwithobjects.files.wordpress.com/2013/07/zeroconf.png?w=625" width="625" height="337" /></a>

<h2>Customizing our ZeroConf scripts</h2>

As you can see in the picture, in order to customize the ZeroConf scripts, you have to create your own subclasses and overriding the correct hooks.

<h3>Customizing an image script</h3>
A custom image script is defined by a subclass of ZeroConfImageScript. ZeroConfImageScript already defines some image common behavior, such as the <strong>release</strong> number, which we will use in our script.

[code language="ruby"]
ZeroConfImageScript subclass: #OzZeroConfImageScript
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'OurZeroConf'
[/code]

Then we override the methods that tell some information about the files to download:
<ul>
<li><strong>OzZeroConfImageScript&gt;&gt;imageName</strong>: The name of the image and changes files</li>
<li><strong>OzZeroConfImageScript&gt;&gt;imageUrl</strong>: The url of the image zip, instance of ZnUrl</li>
<li><strong>OzZeroConfImageScript&gt;&gt;defaultBaseName</strong>: The baseName of the script</li>
<li><strong>OzZeroConfImageScript class&gt;&gt;baseUrl</strong>: The base url where the scripts are found, instance of ZnUrl</li>
<li><strong>OzZeroConfImageScript class&gt;&gt;fileBaseUrl</strong>: The base url where the files the scripts will download are found, instance of ZnUrl</li>
</ul>

I implemented them in my class as follows
[code language="ruby"]
OzZeroConfImageScript&gt;&gt;imageName
	^'Oz'

OzZeroConfImageScript&gt;&gt;imageUrl
	^ self fileBaseUrl / 'image' / self release asString / self imageFileName / 'download'

OzZeroConfImageScript&gt;&gt;imageFileName
	^'Oz-image-', self release asString , '.zip'

OzZeroConfImageScript&gt;&gt;defaultBasename
	^ self imageName, self release

OzZeroConfImageScript class&gt;&gt;fileBaseUrl
	^ 'https://sourceforge.net/projects/ozobjectspaces/files' asZnUrl

OzZeroConfImageScript class&gt;&gt;baseUrl
	^ self fileBaseUrl / 'get'
[/code]

I also extended my script so it generates a custom html title and uses my combining script when combining:
[code language="ruby"]
OzZeroConfImageScript&gt;&gt;htmlTitle
	^ self imageName, ' Zeroconf Script'

OzZeroConfImageScript&gt;&gt;defaultCombiningScript
	^ OzZeroConfCombinedScript
[/code]

Finally, I created a convenience method for creating a script corresponding to the 1.0 version of my custom image.
[code language="ruby"]
OzZeroConfImageScript class&gt;&gt;oz10
	^self new
		release: '1.0';
		yourself
[/code]

Now you can try generating your script in a workspace,
[code language="ruby"]
OzZeroConfImageScript oz10 generate
[/code]

and see the generated results in your working directory!

<h3>Customizing a vm script</h3>
A custom vm script is defined by a subclass of ZeroConfVMScript. ZeroConfVMScript defines, as its image friend, some vm common behavior, such as the <strong>release</strong> number and virtual machine <strong>type</strong>(i.e., if it is a jitted vm or not), which we will use in our script.

[code language="ruby"]
ZeroConfVMScript subclass: #OzZeroConfVMScript
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'OurZeroConf'
[/code]

Then we override the methods that tell some information about the files to download:
<ul>
<li><strong>OzZeroConfVMScript&gt;&gt;binaryName</strong>: The name of the vm binary name</li>
<li><strong>OzZeroConfVMScript&gt;&gt;binaryNameLinux</strong>: The name of the vm binary name in linux, which tends to be different</li>
<li><strong>OzZeroConfVMScript&gt;&gt;vmUrl</strong>: The url of the vm zip, instance of ZnUrl</li>
<li><strong>OzZeroConfVMScript&gt;&gt;defaultBaseName</strong>: The baseName of the script</li>
<li><strong>OzZeroConfVMScript class&gt;&gt;baseUrl</strong>: The base url where the scripts are found, instance of ZnUrl</li>
<li><strong>OzZeroConfVMScript class&gt;&gt;fileBaseUrl</strong>: The base url where the files the scripts will download are found, instance of ZnUrl</li>
</ul>

I implemented them in my class as follows
[code language="ruby"]
OzZeroConfVMScript&gt;&gt;binaryName
	^'ozstack'

OzZeroConfVMScript&gt;&gt;binaryNameLinux
	^self binaryName

OzZeroConfVMScript&gt;&gt;vmUrl
	^self fileBaseUrl asString, '/vm/', self release asString,'/', self vmFileName, '/download'

OzZeroConfVMScript&gt;&gt;vmFileName
	^'OzVm-${OS}-', self release asString , '.zip'

OzZeroConfVMScript class&gt;&gt;fileBaseUrl
	^ 'https://sourceforge.net/projects/ozobjectspaces/files' asZnUrl

OzZeroConfVMScript class&gt;&gt;baseUrl
	^ self fileBaseUrl / 'get'
[/code]

I also extended my script so it uses my combining script when combining:
[code language="ruby"]
OzZeroConfVMScript&gt;&gt;defaultCombiningScript
	^ OzZeroConfCombinedScript
[/code]

Finally, I created a convenience method for creating a script corresponding to the 1.0 version of my custom vm.
[code language="ruby"]
OzZeroConfVMScript class&gt;&gt;ozvm10
	^self new
		type: 'oz';
		release: '1.0';
		yourself
[/code]

Now you can try generating your script in a workspace,
[code language="ruby"]
OzZeroConfVMScript ozvm10 generate
[/code]

and see the generated results in your working directory!

<h3>Customizing a combined script</h3>
A combined script is the one we use to combine several scripts. It is defined by a subclass of ZeroConfCombinedScript.

[code language="ruby"]
ZeroConfCombinedScript subclass: #OzZeroConfCombinedScript
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'OurZeroConf'
[/code]

Then we override the methods that tell some information about the files to download:
<ul>
<li><strong>OzZeroConfCombinedScript class&gt;&gt;baseUrl</strong>: The base url where the scripts are found, instance of ZnUrl</li>
<li><strong>OzZeroConfCombinedScript class&gt;&gt;fileBaseUrl</strong>: The base url where the files the scripts will download are found, instance of ZnUrl</li>
</ul>

I implemented them in my class as follows
[code language="ruby"]
OzZeroConfCombinedScript class&gt;&gt;fileBaseUrl
	^ 'https://sourceforge.net/projects/ozobjectspaces/files' asZnUrl

OzZeroConfCombinedScript class&gt;&gt;baseUrl
	^ self fileBaseUrl / 'get'
[/code]

As you can see, my methods baseUrl and fileBaseUrl are always the same in all scripts. I extracted them into another class later, but keep the code here as is for clarity.

I also extended my script so it uses my combining script when combining and the html title:
[code language="ruby"]
OzZeroConfCombinedScript&gt;&gt;defaultCombiningScript
	^ OzZeroConfCombinedScript

OzZeroConfCombinedScript&gt;&gt;htmlTitle
	^ self scripts first htmlTitle
[/code]

<h2>Integrating everything and automating generation</h2>

As I already showed you, every script understands the message <strong>#generate</strong> to generate itself. However, we may want to generate many scripts, and combine them. The ZeroConf infrastructure already provides for that the ZeroConfCommandLineHandler. The ZeroConfCommandLineHandler is a command line handler that knows which are the scripts we want to generate, combines them appropriately and generates them. So we will subclass from ZeroConfCommandLineHandler and specialize it to fulfill our needs.

[code language="ruby"]
ZeroConfCommandLineHandler subclass: #OzZeroConfCommandLineHandler
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'OzZeroConf'
[/code]

Once we have it, we configure it as a command line handler specifying its command name and description:
[code language="ruby"]
OzZeroConfCommandLineHandler class&gt;&gt;commandName
	^ 'ozzeroconf'

OzZeroConfCommandLineHandler class&gt;&gt;description
	^ 'Generate Oz zeroconf bash scripts'
[/code]

And finally we specialize it to tell it about our scripts:
[code language="ruby"]
OzZeroConfCommandLineHandler&gt;&gt;defaultScript
	^ self defaultImage, self defaultVM

OzZeroConfCommandLineHandler&gt;&gt;defaultImage
	^ OzZeroConfImageScript oz10

OzZeroConfCommandLineHandler&gt;&gt;defaultVM
	^ OzZeroConfVMScript ozvm10

OzZeroConfCommandLineHandler&gt;&gt;imageScripts
	^ { 
		OzZeroConfImageScript oz10.
	}

OzZeroConfCommandLineHandler&gt;&gt;vmScripts
	^ { 
		OzZeroConfVMScript ozvm10
	}

OzZeroConfCommandLineHandler&gt;&gt;indexScriptExamplesHtml
	^ ''
[/code]

Now we have our command line handler, we can test it and make it generate our scripts:

[code language="ruby"]
OzZeroConfCommandLineHandler new
	commandLine: CommandLineArguments new;
	generateScripts
[/code]

Finally, if you have all this code in your image, you can just activate it through the command line thanks to the command line handler!

[code language="bash"]
./pharo PharoMyZeroConf.image ozzeroconf
[/code]

And look at the results. Upload your files, archive them, and use them :).

Guille
